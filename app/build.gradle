apply plugin: 'com.android.application'

def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'tag', '--list'
            standardOutput = code
        }
        return code.toString().split("\n").size()
    }
    catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}


android {
    signingConfigs {
        release {
            keyAlias 'android'
            keyPassword System.getenv("KEYPWD")
            storeFile file(System.getenv('HOME') + '/.keystore-android.jks')
            storePassword System.getenv("KSTOREPWD")
        }
    }
    compileSdkVersion 23
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId 'com.pbrane.mike.ipvxsubnet'
        minSdkVersion 22
        targetSdkVersion 23
        versionName getVersionName()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def vstr = variant.versionName.toString().split("-")
                    def appName = "ipvxsubnet-" + vstr[0] + "-" + vstr[1]
                    output.outputFile = new File(output.outputFile.parent,
                            output.outputFile.name.replace("app-release", appName))
                }
            }
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.0.0'
}
